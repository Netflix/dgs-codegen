import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

/*
 *
 *  Copyright 2020 Netflix, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

plugins {
    id 'org.jetbrains.kotlin.jvm' version "2.0.21" apply false
    id 'com.netflix.nebula.netflixoss' version '11.4.0'
    id "org.jmailen.kotlinter" version "5.0.1"
}

description = 'Netflix GraphQL DGS Code Generation'

allprojects {

    ext {
        Versions = [
                KOTLIN_VERSION: '2.1.21'
        ]
    }

    repositories {
        mavenCentral()
    }

    apply plugin: 'com.netflix.nebula.netflixoss'
    apply plugin: 'com.netflix.nebula.info'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = 'com.netflix.graphql.dgs.codegen'

    dependencies {
        // Gradle Platforms applied to all modudles
        api platform("com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:10.0.4")

        implementation platform("org.jetbrains.kotlin:kotlin-bom:${Versions.KOTLIN_VERSION}")
        implementation platform("com.fasterxml.jackson:jackson-bom:2.18.3")

        // Test dependencies
        testImplementation platform('org.junit:junit-bom:5.10.+')
        testImplementation 'org.jetbrains.kotlin:kotlin-compiler'
        testImplementation 'org.assertj:assertj-core:3.27.+'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'

        testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }
}

subprojects {
    apply {
        plugin("org.jmailen.kotlinter")
    }
}
