/*
 *
 *  Copyright 2020 Netflix, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.12.0'
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: 'nebula.netflixoss'
apply plugin: 'license'

dependencies {
    api project(":graphql-dgs-codegen-core")
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.31"

    testImplementation gradleTestKit()
    testImplementation("com.google.guava:guava:30.1-jre")
    testImplementation("org.assertj:assertj-core:3.11.+")
    testImplementation(platform("org.junit:junit-bom:5.7.+"))
    testImplementation("org.junit.jupiter:junit-jupiter")
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {

    }
}

description = 'Netflix GraphQL DGS Code Generation Plugin'

pluginBundle {
    vcsUrl = 'https://github.com/Netflix/dgs-codegen.git'
    description = 'Gradle plugin for generating Java and Kotlin types from GraphlQL Schema'
    website = 'https://github.com/Netflix/dgs-codegen'
    plugins {
        codegen {
            id = 'com.netflix.dgs.codegen'
            displayName = 'DGS Code Generation Plugin'
            description = 'Gradle plugin for generating Java and Kotlin classes from GraphQL Schema'
            tags = ['Codegen']
        }
    }
}

group = 'com.netflix.graphql.dgs.codegen'
gradlePlugin {
    // Define the plugin
    plugins {
        codegen {
            id = 'com.netflix.dgs.codegen'
            implementationClass = 'com.netflix.graphql.dgs.codegen.gradle.CodegenPlugin'
            displayName = 'DGS Code Generation Plugin'
        }
    }
}

license {
    exclude('**/*.bin')
    exclude('**/test-project/*')
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

test {
    useJUnitPlatform()
}

task createClasspathManifest {
    File outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    inputs.files project(":graphql-dgs-codegen-core").sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join('\n') + project(":graphql-dgs-codegen-core").sourceSets.main.runtimeClasspath.join('\n')
    }
}

publishPlugins.dependsOn tasks.check
gradle.taskGraph.whenReady { graph ->
    tasks.publishPlugins.onlyIf {
        graph.hasTask(':final')
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntimeOnly files(createClasspathManifest)
}

project.afterEvaluate {
    //Disable marker tasks
    project.tasks.findAll {
        (it.name.contains("Marker") && it.name.contains('Maven')) ||
                it.name.contains("PluginMarkerMavenPublicationToNetflixOSSRepository") ||
                it.name.contains("PluginMarkerMavenPublicationToSonatypeRepository") ||
                it.name.contains("publishPluginMavenPublicationToNetflixOSSRepository") ||
                it.name.contains("publishPluginMavenPublicationToSonatypeRepository")
    }.each {
        it.enabled = false
    }
}

TaskProvider validatePluginsTask = project.tasks.named('validatePlugins')
TaskProvider publishPluginsTask = project.tasks.named('publishPlugins')
project.plugins.withId('nebula.release') {
    project.tasks.withType(PublishToMavenRepository).configureEach {
        def releasetask = project.rootProject.tasks.findByName('release')
        if (releasetask) {
              it.mustRunAfter(releasetask)
              it.dependsOn(validatePluginsTask)
              it.dependsOn(publishPluginsTask)
        }
    }
}
